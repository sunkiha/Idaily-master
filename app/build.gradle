apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def ext = rootProject.ext

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion ext.compileSdkVersion
    buildToolsVersion ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId ext.applicationId
        minSdkVersion ext.minSdkVersion
        targetSdkVersion ext.targetSdkVersion
        versionCode ext.versionCode
        versionName ext.versionName
        multiDexEnabled true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        release {
            try {
                storeFile file("../daily.jks")
                storePassword KEYSTORE_PWD
                keyAlias KEY_ALIAS
                keyPassword KEY_PWD
            } catch (ex) {
                throw new InvalidUserDataException("Have to define KEYSTORE_PWD and KEY_PWD.")
            }
        }
    }

    productFlavors {
        daily {}
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            zipAlignEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // goin_v1.0.0_2015-07-30_xiaomi.apk
                        def fileName = "daily_v${ext.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    dataBinding {
        enabled = true
    }

}

dependencies {

    def compiles = ext.compiles
    def provides = ext.provides
    def apts = ext.apts

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile compiles.supportAppCompat
    compile compiles.supportRecyclerView
    compile compiles.supportDesgin
    compile compiles.supportCardView
    compile compiles.glide
    compile compiles.logansquare
    compile compiles.viewpagerIndicator
    compile compiles.guangqiangWidgets
    compile compiles.guangqiangUtils
    compile compiles.retrofit
    compile compiles.retrofitAdapter
    compile compiles.okhttpLog
    compile compiles.logansquareConverter
    compile compiles.rxandroid
    compile compiles.rxjava
    compile compiles.eventbus
    compile compiles.dagger

    provided provides.daggerCompiler
    provided provides.annotation

    apt apts.logansquareCompiler
}
